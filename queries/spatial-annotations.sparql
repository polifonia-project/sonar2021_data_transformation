PREFIX core:  <https://w3id.org/polifonia/ON/core/>
PREFIX pr:    <https://w3id.org/polifonia/resource/>
PREFIX fx:    <http://sparql.xyz/facade-x/ns/>
PREFIX mp:    <https://w3id.org/polifonia/ON/musical-performance/>
PREFIX rdf:   <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX xyz:   <http://sparql.xyz/facade-x/data/>
PREFIX rdfs:  <http://www.w3.org/2000/01/rdf-schema#>

SELECT 
  ?recordingID 
  ?recordingTitleLabel 
  ?performerID 
  ?performerLabel ?youtubeID ?sessionType ?placeLabel ?placeFullAddress ?placeLat ?placeLong ?sessionTypeLabel
WHERE {

  ?recordingID rdf:type mp:Recording ;
      core:hasTitle ?title ;
      core:hasYoutubeID ?youtubeID ;
      mp:hasRecordingPerformer ?performerID ;
      mp:isRecordingProducedBy ?recordingProcess
  .
  ?title rdfs:label ?recordingTitleLabel.
  ?performerID rdfs:label ?performerLabel.

  ?recordingProcess mp:hasSession ?session.
  ?session core:hasPlace ?place ;
           core:hasType ?sessionType.

  ?sessionType rdfs:label ?sessionTypeLabel .

  ?place rdfs:label ?placeLabel ;
         core:hasAddress ?placeAddress ;
         core:hasGeometry ?placeGeometry
  .

  ?placeAddress core:fullAddress ?placeFullAddress.
  ?placeGeometry core:lat ?placeLat ;
                 core:long ?placeLong
  .

}